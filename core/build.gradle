plugins {
	id "com.moowork.node" version "0.7"
	id "com.jfrog.bintray" version "1.0"
}

repositories {
	mavenLocal()
	jcenter()
	mavenCentral()
}

group = 'com.craigburke.angular'
version = '2.1.0'

apply plugin: 'groovy'
apply plugin: 'maven-publish'

configurations { 
	provided 
	testCompile { extendsFrom provided }
}

node {
	version = '0.11.10'
	distBaseUrl = 'http://nodejs.org/dist'
	download = true
	workDir = file("${project.buildDir}/nodejs")
}

project.ext {
	tempFile = file('ngannotate.js')
	destination = file('src/main/resources/')
	npmDependencies = ['ng-annotate', 'browserify']
}
 
sourceSets {
	main {
		compileClasspath += configurations.provided
	}
} 
 
dependencies {
	compile 'com.bertramlabs.plugins:asset-pipeline-core:2.0.12'
	provided 'org.codehaus.groovy:groovy-all:2.3.6'

	compile 'org.mozilla:rhino:1.7R4'
	
	testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
}

task installDependencies(type: NpmTask) {
	args = ['install'] + npmDependencies + ['--save-dev']
}

task bundleNgAnnotate(type: NodeTask, dependsOn: 'installDependencies') {
	script = file('node_modules/browserify/bin/cmd.js')
	args = ['node_modules/ng-annotate/build/es5/ng-annotate-main.js', '-o', tempFile.path]
}

task installNgAnnotate(dependsOn: 'bundleNgAnnotate') << {
	tempFile.text = "var ngAnnotate;${tempFile.text}"
	
	copy {
		from tempFile
		into destination
		filter { String line ->
			line.replace('module.exports = function ngAnnotate', 'ngAnnotate = function ngAnnotate')
		}
	}
	
	delete { tempFile }
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.1'
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

bintray {
    user = project.hasProperty('bintrayUsername') ? project.bintrayUsername : ''
    key = project.hasProperty('bintrayApiKey') ? project.bintrayApiKey : ''
    publications = ['maven']  
	pkg {
		repo = 'asset-pipeline'
		userOrg = 'craigburke'
		name = 'angular-annotate-asset-pipeline'
		licenses = ['Apache-2.0']
	}
}


publishing {
	publications {
		maven(MavenPublication) {
			artifactId 'angular-annotate-asset-pipeline'
			pom.withXml {
				asNode().children().last() + {
					resolveStrategy = Closure.DELEGATE_FIRST
					name 'angular-annotate-asset-pipeline'
                    description 'AngularJS Annotation extension for the Asset Pipeline Library.'
					url 'https://github.com/craigburke/angular-annotate-asset-pipeline'
					scm {
						url 'https://github.com/craigburke/angular-annotate-asset-pipeline'
						connection 'scm:https://github.com/craigburke/angular-annotate-asset-pipeline.git'
						developerConnection 'scm:https://github.com/craigburke/angular-annotate-asset-pipeline.git'
                    }
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/license/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'craigburke'
                            name 'Craig Burke'
                            email 'craig@craigburke.com'
                        	}
                    	}
					}
			}		
			from components.java
			artifact sourcesJar
            artifact javadocJar
		}
	}
}

bintrayUpload.dependsOn build, sourcesJar, javadocJar
